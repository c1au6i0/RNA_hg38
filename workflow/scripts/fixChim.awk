#!/usr/bin/awk -f

# This script adds a "chr" prefix to the Chimeric.out.junction file
# generated by STAR, because STAR-Fusion needs this prefix.

# chimeric junction file is tab-separated
BEGIN { FS = OFS = "\t" }

# find chromosome columns
NR == 1 {
	for (i = 1; i <= NF; i++)
		if ($i ~ /^chr_/)
			chr_cols[i] = 1
}

# fix chromosome names
NR > 1 && !(/^#/) {
	for (chr in chr_cols) {
		if ($chr ~ /^([0-9]+|[XY])$/) $chr = "chr" $chr
		if ($chr == "MT") $chr = "chrM"
	}
}

# print fixed line
{ print }

